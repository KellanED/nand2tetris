// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
// Kellan Delaney
// 06/16/2020

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Mux16(a=x, b=false, sel=zx, out=zerx);           // zero x or not
    Not16(in=zerx, out=negx);                        // negate x
    Mux16(a=zerx, b=negx, sel=nx, out=xout);         // choose x or neg x
    Mux16(a=y, b=false, sel=zy, out=zery);           // zero y or not
    Not16(in=zery, out=negy);                        // negate y
    Mux16(a=zery, b=negy, sel=ny, out=yout);         // choose y or neg y
    And16(a=xout, b=yout, out=xandy);                // x and y
    Add16(a=xout, b=yout, out=xplusy);               // x + y
    Mux16(a=xandy, b=xplusy, sel=f, out=nonnegout);  // choose output
    Not16(in=nonnegout, out=negout);                 // negate output
    Mux16(a=nonnegout, b=negout, sel=no, out=oo);    // choose to negate output
    And16(a=oo, b=true, out[15]=ng);                 // ng
    And16(a=oo, b=true, out[0..7]=outhalf1, out[8..15]=outhalf2); //split up out
    Or8Way(in=outhalf1, out=nonzero1);               // check if first half nonzero
    Or8Way(in=outhalf2, out=nonzero2);               // check if second half nonzero
    Or(a=nonzero1, b=nonzero2, out=nonzero);         // combine to see if all nonzero
    Not(in=nonzero, out=zr);                         // zr
    Or16(a=oo, b=false, out=out);                    // oo=out
}